# Group 2 #
.data
displayBuffer:  .space 0x40000 # space for 512x256 bitmap display 
errorBuffer:    .space 0x40000 # space to store match function
templateBuffer: .space 0x100   # space for 8x8 template
imageFileName:    .asciiz "pxlcon512x256cropgs.raw" 
templateFileName: .asciiz "template8x8gs.raw"
# struct bufferInfo { int *buffer, int width, int height, char* filename }
imageBufferInfo:    .word displayBuffer  512 128  imageFileName
errorBufferInfo:    .word errorBuffer    512 128  0
templateBufferInfo: .word templateBuffer 8   8    templateFileName


.text
main:	la $a0, imageBufferInfo #load the image struct into $a0
	jal loadImage 
	la $a0, templateBufferInfo #load the template struct into $a0
	jal loadImage
	la $a0, imageBufferInfo #load the image struct into $a0
	la $a1, templateBufferInfo #load the template struct into $a1
	la $a2, errorBufferInfo #load the error struct into $a1
	jal matchTemplate        # MATCHING DONE HERE
	#jal matchTemplateFast
	la $a0, errorBufferInfo # after match template is complete, the error buffer will be full
				# of the errors of each 8x8 section of the image compared with the 
				#8x8 template, we load this buffer into the find best function
				# and in $v0 will be the offset of the best error
	jal findBest
	la $a0, imageBufferInfo
	move $a1, $v0
	jal highlight #after loading the offset and the imsage, highlight the correct block
	la $a0, errorBufferInfo	
	jal processError # makes a green dot at the best error
	li $v0, 10		# exit
	syscall
	

##########################################################
# matchTemplate( bufferInfo imageBufferInfo, bufferInfo templateBufferInfo, bufferInfo errorBufferInfo )
# NOTE: struct bufferInfo { int *buffer, int width, int height, char* filename }
matchTemplate:	
	
	# TODO: write this function!
	
	lw $t0 0($a0) # load address of displayBuffer (0x10010000) into $t0
	lw $t1 4($a0) # load width of image (512) into $t1
	lw $t2 8($a0) # load height of image (128) into $t2 
	lw $t3, 0($a1) # load base address of template buffer
	lw $t4, 0($a2) # load base address of error buffer

	add $t5, $zero, $zero # $t5 = y iterator
	subi $a2, $t2, 7	# acceprable for convention? # need to stop 7 columns early because our pointer is in the "top left"
	subi $a3, $t1, 7	#acceptable for convention?  # need to stop 7 rows early because our pointer is in the "top left"
	
y_loop:
	bge $t5, $a2, end_y_loop 
	add $t6, $zero, $zero # $t6 = x iterator
	
x_loop:
	bge $t6, $a3, next_y
	
	add $t7, $zero, $zero # initialize sum of abs difference
	
	add $t8, $t3, $zero 	# template address
	add $t9, $zero, $zero   # template row index
	
template_y_loop:
	bge $t9, 8, finish_pixel
	
	# loop through template y
	add $s0, $zero, $zero	# use $s0 respecting convention ?

template_x_loop:
	bge $s0, 8, next_row
	
	# Compute SAD template/image
	mul $s1, $t9, $t1     # y offset from template comparison
    	mul $s2, $t5, $t1     # y offset from pixel iteration
    	add $s1, $s1, $s2     # combine both y offsets
    	add $s1, $s1, $t6     # x offset
    	add $s1, $s1, $s0     # x offset from template loop 
	sll $s1, $s1, 2       # Convert to byte offset
    	add $s1, $s1, $t0     # add image base address

    	lbu $s2, 0($s1)        # load image pixel value
	
	
	# Template pixel access
	mul $s3, $t9, 8         # Row offset in the template (row * 8)
	add $s3, $s3, $s0       # Add column offset
	sll $s3, $s3, 2         # Convert to byte offset
	add $s3, $s3, $t8       # Add base address of the template buffer
	lbu $s4, 0($s3)         # load template pixel value

    	sub $s5, $s2, $s4     # Compute difference
    	abs $s5, $s5          # Take absolute value
    	add $t7, $t7, $s5     # Accumulate error

    	addi $s0, $s0, 1      # Increment column index
    	j template_x_loop
	
	
next_row:
	addi $t9, $t9, 1      # Increment row index
    	j template_y_loop

finish_pixel:
	# Need to store final error in error buffer 
	mul $s1, $t5, $t1     # Row offset
    	add $s1, $s1, $t6     # Add column offset
    	sll $s1, $s1, 2       # Convert to byte offset
    	add $s1, $s1, $t4     # Add error buffer base address
    	sw $t7, 0($s1)        # Store SAD

    	addi $t6, $t6, 1      # Increment x-coordinate
    	j x_loop
	
next_y:
	addi $t5, $t5, 1	# increment y
	j y_loop
	
end_y_loop:


	# set all $sn registers back to 0
	
	jr $ra	
	
##########################################################
# matchTemplateFast( bufferInfo imageBufferInfo, bufferInfo templateBufferInfo, bufferInfo errorBufferInfo )
# NOTE: struct bufferInfo { int *buffer, int width, int height, char* filename }
matchTemplateFast:	
	
	# TODO: write this function!
	
	lw $s0, 0($a0) #Image buffer base address
	lw $s1, 0($a2) #Error buffer base address
	lw $s2, 0($a1) #Target buffer base address
	
	lw $s3, 4($a0) # Width
	lw $s4, 8($a0) #Height
	
	add $v0 $0 $0 #set j to 0 before it's loop begins
	
	#for loop here j = 0; j < 8; j++, $v0 register holds j
	jLoop:
	
		add $v1 $0 $0 #reset y to 0 before it's loop begins
	
		mul $t8, $v0, 8 #row offset 8*j
		sll $t8, $t8, 2 #multiply by 4 because we store 4 bytes at a time
		add $t9, $s2, $t8 #$t9 stores T(0,j)
	
		addi $t0, $t9, 0 #storing the first row of the template in 8 registers
		lbu $t0, 0($t0) #loading the byte stored at T(0, j)
		
		addi $t1, $t9, 4
		lbu $t1, 0($t1) #loading the byte stored at T(1, j)
		
		addi $t2, $t9, 8
		lbu $t2, 0($t2) #loading the byte stored at T(2, j)
		
		addi $t3, $t9, 12
		lbu $t3, 0($t3) #loading the byte stored at T(3, j)
		
		addi $t4, $t9, 16
		lbu $t4, 0($t4) #loading the byte stored at T(4, j)
		
		addi $t5, $t9, 20
		lbu $t5, 0($t5) #loading the byte stored at T(5, j)
		
		addi $t6, $t9, 24
		lbu $t6, 0($t6) #loading the byte stored at T(6, j)
		
		addi $t7, $t9, 28
		lbu $t7, 0($t7) #loading the byte stored at T(7, j)
		
		
		#for loop here y = 0; y <= height - 8; y++, $v1 register holds y
		yLoop:
		
		add $a3 $0 $0 #reset x to 0 before it's loop begins
			#for loop here x = 0; x <= width - 8; x++, $a3 register holds x
			
			xLoop:
			mul $t8, $v1 $s3 # y*width which is the row offset
			add $t8, $t8, $a3 #plus column offset
			sll $t8, $t8, 2 #multiply by 4 because we store 4 bytes at a time
			
			
			
			add $s5, $s1, $t8 #$s5 now stores E(x,y)
			
			add $t8, $v1, $v0 
			mul $t8, $t8, $s3 # (y + j)*width which is the row offset
			add $t8, $t8, $a3 #plus column offset
			sll $t8, $t8, 2 #multiply by 4 because we store 4 bytes at a time
			add $s6, $s0, $t8 #$s6 stores I(x,y+j)
			
			#i'm going to use $t8 to store the accumulation of the error at E(x,y)
			add $t8, $0, $0
			
			#------------------------------
			
			add $s6, $s6, 0
			lbu $s7, 0($s6) #loading the byte stored at I(x + 0, y +j)
			sub $s7, $s7, $t0
			abs $s7, $s7
			
			add $t8, $t8, $s7 #accumulate the absolute error at E(x,y)
			
			
			
			#-----------------------------
			
			add $s6, $s6, 4
			lbu $s7, 0($s6) #loading the byte stored at I(x + 1, y +j)
			sub $s7, $s7, $t1
			abs $s7, $s7
			#sw $s7, 0($s5) 
			add $t8, $t8, $s7 #accumulate the absolute error at E(x,y)
			
			#----------------------------
			
			add $s6, $s6, 4
			lbu $s7, 0($s6) #loading the byte stored at I(x + 2, y +j)
			sub $s7, $s7, $t2
			abs $s7, $s7
			#sw $s7, 0($s5) 
			add $t8, $t8, $s7 #accumulate the absolute error at E(x,y)
			
			#----------------------------
			
			add $s6, $s6, 4
			lbu $s7, 0($s6) #loading the byte stored at I(x + 3, y +j)
			sub $s7, $s7, $t3
			abs $s7, $s7
			#sw $s7, 0($s5) 
			add $t8, $t8, $s7 #accumulate the absolute error at E(x,y)
			
			#----------------------------
			
			add $s6, $s6, 4
			lbu $s7, 0($s6) #loading the byte stored at I(x + 4, y +j)
			sub $s7, $s7, $t4
			abs $s7, $s7
			#sw $s7, 0($s5) 
			add $t8, $t8, $s7 #accumulate the absolute error at E(x,y)
			
			#----------------------------
			
			add $s6, $s6, 4
			lbu $s7, 0($s6) #loading the byte stored at I(x + 5, y +j)
			sub $s7, $s7, $t5
			abs $s7, $s7
			#sw $s7, 0($s5) 
			add $t8, $t8, $s7 #accumulate the absolute error at E(x,y)
			
			#----------------------------
			
			add $s6, $s6, 4
			lbu $s7, 0($s6) #loading the byte stored at I(x + 6, y +j)
			sub $s7, $s7, $t6
			abs $s7, $s7
			#sw $s7, 0($s5)
			add $t8, $t8, $s7 #accumulate the absolute error at E(x,y) 
			
			#----------------------------
			
			add $s6, $s6, 4
			lbu $s7, 0($s6) #loading the byte stored at I(x + 7, y +j)
			sub $s7, $s7, $t7
			abs $s7, $s7
			#sw $s7, 0($s5) 
			add $t8, $t8, $s7 #accumulate the absolute error at E(x,y)
			
			lw $t9, 0($s5) #save the absolute error that was already there
			add $t8, $t8, $t9 #add it to the accumulated error of this specific run
			
			sw $t8, 0($s5) 
			
			addi $a3, $a3, 1 #x++
			addi $t8, $s3, -8 #width - 8
			ble $a3, $t8, xLoop
		
		addi $v1, $v1, 1 #y++
		addi $t8, $s4, -8 #height - 8
		ble $v1, $t8, yLoop
	
	addi $v0, $v0, 1 #j++
	blt $v0, 8, jLoop
	
jr $ra	
	
	
	
###############################################################
# loadImage( bufferInfo* imageBufferInfo )
# NOTE: struct bufferInfo { int *buffer, int width, int height, char* filename }
loadImage:	lw $a3, 0($a0)  # int* buffer
		lw $a1, 4($a0)  # int width
		lw $a2, 8($a0)  # int height
		lw $a0, 12($a0) # char* filename
		mul $t0, $a1, $a2 # words to read (width x height) in a2
		sll $t0, $t0, 2	  # multiply by 4 to get bytes to read
		li $a1, 0     # flags (0: read, 1: write)
		li $a2, 0     # mode (unused)
		li $v0, 13    # open file, $a0 is null-terminated string of file name
		syscall
		move $a0, $v0     # file descriptor (negative if error) as argument for read
  		move $a1, $a3     # address of buffer to which to write
		move $a2, $t0	  # number of bytes to read
		li  $v0, 14       # system call for read from file
		syscall           # read from file
        		# $v0 contains number of characters read (0 if end-of-file, negative if error).
        		# We'll assume that we do not need to be checking for errors!
		# Note, the bitmap display doesn't update properly on load, 
		# so let's go touch each memory address to refresh it!
		move $t0, $a3	   # start address
		add $t1, $a3, $a2  # end address
loadloop:	lw $t2, ($t0)
		sw $t2, ($t0)
		addi $t0, $t0, 4
		bne $t0, $t1, loadloop
		jr $ra
		
		
#####################################################
# (offset, score) = findBest( bufferInfo errorBuffer )
# Returns the address offset and score of the best match in the error Buffer
findBest:	lw $t0, 0($a0)     # load error buffer start address	
		lw $t2, 4($a0)	   # load width
		lw $t3, 8($a0)	   # load height
		addi $t3, $t3, -7  # height less 8 template lines minus one
		mul $t1, $t2, $t3
		sll $t1, $t1, 2    # error buffer size in bytes	
		add $t1, $t0, $t1  # error buffer end address
		li $v0, 0		# address of best match	
		li $v1, 0xffffffff 	# score of best match	
		lw $a1, 4($a0)    # load width
        		addi $a1, $a1, -7 # initialize column count to 7 less than width to account for template
fbLoop:		lw $t9, 0($t0)        # score
		sltu $t8, $t9, $v1    # better than best so far?
		beq $t8, $zero, notBest
		move $v0, $t0
		move $v1, $t9
notBest:		addi $a1, $a1, -1
		bne $a1, $0, fbNotEOL # Need to skip 8 pixels at the end of each line
		lw $a1, 4($a0)        # load width
        		addi $a1, $a1, -7     # column count for next line is 7 less than width
        		addi $t0, $t0, 28     # skip pointer to end of line (7 pixels x 4 bytes)
fbNotEOL:	add $t0, $t0, 4
		bne $t0, $t1, fbLoop
		lw $t0, 0($a0)     # load error buffer start address	
		sub $v0, $v0, $t0  # return the offset rather than the address
		jr $ra
		

#####################################################
# highlight( bufferInfo imageBuffer, int offset )
# Applies green mask on all pixels in an 8x8 region
# starting at the provided addr.
highlight:	lw $t0, 0($a0)     # load image buffer start address
		add $a1, $a1, $t0  # add start address to offset
		lw $t0, 4($a0) 	# width
		sll $t0, $t0, 2	
		li $a2, 0xff00 	# highlight green
		li $t9, 8	# loop over rows
highlightLoop:	lw $t3, 0($a1)		# inner loop completely unrolled	
		and $t3, $t3, $a2
		sw $t3, 0($a1)
		lw $t3, 4($a1)
		and $t3, $t3, $a2
		sw $t3, 4($a1)
		lw $t3, 8($a1)
		and $t3, $t3, $a2
		sw $t3, 8($a1)
		lw $t3, 12($a1)
		and $t3, $t3, $a2
		sw $t3, 12($a1)
		lw $t3, 16($a1)
		and $t3, $t3, $a2
		sw $t3, 16($a1)
		lw $t3, 20($a1)
		and $t3, $t3, $a2
		sw $t3, 20($a1)
		lw $t3, 24($a1)
		and $t3, $t3, $a2
		sw $t3, 24($a1)
		lw $t3, 28($a1)
		and $t3, $t3, $a2
		sw $t3, 28($a1)
		add $a1, $a1, $t0	# increment address to next row	
		add $t9, $t9, -1		# decrement row count
		bne $t9, $zero, highlightLoop
		jr $ra

######################################################
# processError( bufferInfo error )
# Remaps scores in the entire error buffer. The best score, zero, 
# will be bright green (0xff), and errors bigger than 0x4000 will
# be black.  This is done by shifting the error by 5 bits, clamping
# anything bigger than 0xff and then subtracting this from 0xff.
processError:	lw $t0, 0($a0)     # load error buffer start address
		lw $t2, 4($a0)	   # load width
		lw $t3, 8($a0)	   # load height
		addi $t3, $t3, -7  # height less 8 template lines minus one
		mul $t1, $t2, $t3
		sll $t1, $t1, 2    # error buffer size in bytes	
		add $t1, $t0, $t1  # error buffer end address
		lw $a1, 4($a0)     # load width as column counter
        		addi $a1, $a1, -7  # initialize column count to 7 less than width to account for template
pebLoop:		lw $v0, 0($t0)        # score
		srl $v0, $v0, 5       # reduce magnitude 
		slti $t2, $v0, 0x100  # clamp?
		bne  $t2, $zero, skipClamp
		li $v0, 0xff          # clamp!
skipClamp:	li $t2, 0xff	      # invert to make a score
		sub $v0, $t2, $v0
		sll $v0, $v0, 8       # shift it up into the green
		sw $v0, 0($t0)
		addi $a1, $a1, -1        # decrement column counter	
		bne $a1, $0, pebNotEOL   # Need to skip 8 pixels at the end of each line
		lw $a1, 4($a0)        # load width to reset column counter
        		addi $a1, $a1, -7     # column count for next line is 7 less than width
        		addi $t0, $t0, 28     # skip pointer to end of line (7 pixels x 4 bytes)
pebNotEOL:	add $t0, $t0, 4
		bne $t0, $t1, pebLoop
		jr $ra
